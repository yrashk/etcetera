buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.1.0'
        classpath 'com.github.lburgazzoli:gradle-karaf-plugin:0.0.40'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.12.1"
    id 'net.researchgate.release' version '2.3.4'
}

apply plugin: 'license'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library-distribution'
apply plugin: 'biz.aQute.bnd.builder'
apply plugin: "com.github.lburgazzoli.karaf"
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'com.bmuschko.nexus'

license {
    header project.file('LICENSE-HEADER')
}

release {
    tagTemplate = 'v${version}'
    failOnPublishNeeded = false
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
            artifact("${rootProject.buildDir}/karaf/features/${rootProject.name}-${rootProject.version}-features.xml") {
                classifier "features"
            }
        }
    }
}


modifyPom {
    project {
        name 'etcetera'
        description 'Extended Backend for OSGi Configuration Files'
        url 'https://github.com/yrashk/etcetera'
        inceptionYear '2016'

        scm {
            url 'https://github.com/yrashk/etcetera'
            developerConnection 'scm:https://yrashk@github.com/yrashk/etcetera.git'
            connection 'scm:git://github.com/yrashk/etcetera.git'
        }

        licenses {
            license {
                name 'Mozilla Public License, v. 2.0'
                url 'http://mozilla.org/MPL/2.0/'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'yrashk'
                name 'Yurii Rashkovskii'
                email 'yrashk@gmail.com'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        instruction 'Karaf-Commands', '*'
        instruction 'Provide-Capability',
                'osgi.service;objectClass:List<String>="com.yrashk.etcetera.ConfigBackend"'
    }
}


configurations {
    dist
    dist.exclude group: 'org.osgi'
}

dependencies {
    // OSGi
    compileOnly 'org.osgi:org.osgi.core:6.0.0'
    testCompile 'org.osgi:org.osgi.core:6.0.0'
    compileOnly 'org.osgi:org.osgi.service.cm:1.5.0'
    testCompile 'org.osgi:org.osgi.service.cm:1.5.0'
    compileOnly 'org.osgi:org.osgi.service.component:1.3.0'
    testCompile 'org.osgi:org.osgi.service.cm:1.5.0'
    compileOnly 'biz.aQute.bnd:biz.aQute.bndlib:3.2.0'

    compile 'org.apache.felix:org.apache.felix.utils:1.8.2'
    dist 'org.apache.felix:org.apache.felix.utils:1.8.2'

    compile 'com.amazonaws:aws-java-sdk-osgi:1.11.32'
    dist 'com.amazonaws:aws-java-sdk-osgi:1.11.32'

    compile 'com.amazonaws:jmespath-java:1.0'
    dist 'com.amazonaws:jmespath-java:1.0'

    compile 'org.apache.karaf.shell:org.apache.karaf.shell.core:4.0.5'
    compile 'org.apache.karaf.shell:org.apache.karaf.shell.commands:4.0.5'
    compile 'org.apache.karaf.shell:org.apache.karaf.shell.console:4.0.5'
    compile 'org.apache.karaf.shell:org.apache.karaf.shell.table:4.0.5'

    testCompile 'junit:junit:4.12'
    testCompile 'org.slf4j:slf4j-simple:1.7.18'

    testCompile 'org.ops4j.pax.exam:pax-exam:4.9.1'
    testCompile 'org.ops4j.pax.exam:pax-exam-junit4:4.9.1'
    testCompile 'org.ops4j.pax.exam:pax-exam-container-karaf:4.9.1'
    testCompile 'org.ops4j.pax.url:pax-url-aether:2.4.7'
    testCompile 'org.ops4j.pax.url:pax-url-wrap:2.4.7'
    testCompile 'org.ops4j.pax.url:pax-url-link:2.4.7'
    testCompile 'org.ops4j.pax.url:pax-url-classpath:2.4.7'
    testCompile 'javax.inject:javax.inject:1'

}

karaf {
    features {
        xsdVersion = '1.3.0'
        repository "mvn:org.apache.karaf.features/standard/4.0.6/xml/features"


        feature {
            name = 'etcetera'
            includeProject = true

            configurations 'dist'

            feature('scr') {
                version = "4.0.6"
            }

            feature('wrap') {
                prerequisite = true
            }

            outputFile = new File(
                    "${project.buildDir}/karaf/features",
                    "${name}-${version}-features.xml"
            )
        }
    }

    kar {}
}

publishToMavenLocal.dependsOn generateFeatures

test.dependsOn publishToMavenLocal